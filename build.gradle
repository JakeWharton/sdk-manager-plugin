buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  
  dependencies {
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'idea'

sourceSets {
  acceptanceTest { sourceSet ->
    sourceSet.java.srcDir file('src/acceptanceTest/java')
    sourceSet.groovy.srcDir file('src/acceptanceTest/groovy')
  }
}

idea {
  module {
    testSourceDirs += file('src/acceptanceTest/java')
    testSourceDirs += file('src/acceptanceTest/groovy')
  }
}

task acceptanceTest(type: Test) {
  testClassesDir = sourceSets.acceptanceTest.output.classesDir
  classpath = sourceSets.acceptanceTest.runtimeClasspath
}

acceptanceTest.mustRunAfter project.tasks.findByName('test')
acceptanceTest.description = 'Runs the acceptance tests.'
acceptanceTest.group = JavaBasePlugin.VERIFICATION_GROUP

check.dependsOn acceptanceTest

dependencies {
  repositories {
    mavenCentral()
  }

  compile gradleApi()
  compile localGroovy()
  compile 'com.android.tools.build:gradle:1.5.0'
  compile 'org.rauschig:jarchivelib:0.6.0'
  compile 'commons-io:commons-io:2.4'

  testCompile 'org.easytesting:fest-assert-core:2.0M10'

  acceptanceTestCompile sourceSets.main.output
  acceptanceTestCompile sourceSets.test.output
  acceptanceTestCompile configurations.testCompile
  acceptanceTestRuntime configurations.testRuntime
}

group = 'com.jakewharton.sdkmanager'
version = '1.5.0-SNAPSHOT'

install {
  repositories.mavenInstaller {
    pom.artifactId = 'gradle-plugin'
  }
}

uploadArchives {
  repositories.mavenDeployer {
    pom.artifactId = 'gradle-plugin'
  }
}

modifyPom {
  project {
    name 'SDK Manager'
    description 'Gradle plugin which downloads and manages your Android SDK.'
    url 'https://github.com/JakeWharton/sdk-manager-plugin'
    inceptionYear '2014'

    scm {
      url 'https://github.com/JakeWharton/sdk-manager-plugin'
      connection 'scm:git:git://github.com/JakeWharton/sdk-manager-plugin.git'
      developerConnection 'scm:git:ssh://git@github.com/JakeWharton/sdk-manager-plugin.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'jakewharton'
        name 'Jake Wharton'
        email 'jakewharton@gmail.com'
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
  distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
